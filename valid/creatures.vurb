COM struct that represents a creature
sdef Creature
begin
    element name text
    element hp integer
    element energy integer
end

COM initializes a creature struct
pdef initCreature left structtype Creature creature right void
begin
    structindirelemset creature name "Unnamed"
    structindirelemset creature hp 100
    structindirelemset creature energy 50
end

COM inflicts damage to the defender equal to the attacker's energy stat
pdef damageCreature left structtype Creature attacker structtype Creature defender right void
begin
    vdef damage integer
    set damage structindirelemaccess attacker energy
    structindirelemset defender hp left sub structindirelemaccess defender hp damage right
end

COM heals the creature equal to half of their energy stat
pdef healCreature left structtype Creature creature right void
begin
    vdef heal integer
    set heal left div structindirelemaccess creature energy 2 right
    structindirelemset creature hp left add structindirelemaccess creature hp heal right
end

COM prints out a creature's information
pdef printCreature left structtype Creature creature right void
begin
    write "Name: "
    write structindirelemaccess creature name
    write "HP: "
    write structindirelemaccess creature hp
    write "Energy: "
    write structindirelemaccess creature energy
end

COM increases a creature's energy by 20
pdef powerUpCreature left structtype Creature creature right void
begin
    vdef boost integer
    set boost 20
    structindirelemset creature energy left add structindirelemaccess creature energy boost right
end

main
begin
    vdef structtype Creature player
    vdef structtype Creature enemy

    call initCreature left player right
    call initCreature left enemy right

    structelemset player name "Tim the Enchanter"
    structelemset enemy name "Killer Rabbit"

    write "Player Info:"
    call printCreature left player right

    write ""

    write "Enemy Info:"
    call printCreature left enemy right

    write ""

    write left add left add structelemaccess player name " is attacked by " right structelemaccess enemy name right
    call damageCreature left enemy player right

    write ""

    write "Updated Player Info:"
    call printCreature left player right

    write ""

    write left add structelemaccess player name " heals!" right
    call healCreature left player right

    write left add structelemaccess player name " pulls out holy hand grenade!" right
    call powerUpCreature left player right

    write ""

    write "Updated Player Info:"
    call printCreature left player right

    write ""

    write left add structelemaccess enemy name " is blasted by the grenade!" right
    call damageCreature left player enemy right

    write ""

    write "Updated Enemy Info:"
    call printCreature left enemy right
end
